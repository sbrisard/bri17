kill(all);
load(functs);
print("+--------------------------+");
print("| ELEMENT STIFFNESS MATRIX |");
print("+--------------------------+");
print("");
print("NUMBERING OF NODES");
print("------------------");
print("");
print("");
print("NOMENCLATURE");
print("------------");
ndims : 2;
nnodes : 2**ndims;
ndofs : nnodes*ndims;
xyz : firstn([x, y, z], ndims);
abc : firstn([a, b, c], ndims);
uvw : firstn(['u, 'v, 'w], ndims);
print("- coordinates:", xyz);
print("- element sides:", abc);
print("- components of displacement:", uvw);

lame_I : 2*mu*nu/(1-2*nu);

/* Create a list of lists of 1D shape functions, then compute the product. */
shape_functions_list : lreduce(
  lambda([L1, L2], create_list(x1*x2, x1, L1, x2, L2)),
  map(lambda([xi], [1-xi, xi]), xyz/abc));

N : transpose(matrix(shape_functions_list));

dofs_[i, j] := concat(uvw[i], j);
dofs : genmatrix(dofs_, ndims, nnodes);

u : dofs.N;

eps_[i, j] := factor((diff(u[i, 1], xyz[j])+diff(u[j, 1], xyz[i]))/2);
eps : genmatrix(eps_, ndims, ndims);

tr_eps : tracematrix(eps);
sig : lame_I*tr_eps*ident(ndims)+2*mu*eps;

U : tracematrix(sig.eps)/2;
for i:1 thru ndims do U : integrate(U, xyz[i], 0, abc[i]);
display(U);

q : makelist(dofs[quotient(i-1, nnodes)+1, remainder(i-1, nnodes)+1], i, 1, ndofs);

Kq_[i, j] := diff(U, q[i]);
Kq : genmatrix(Kq_, ndofs, 1);
K : coefmatrix(list_matrix_entries(Kq), q);